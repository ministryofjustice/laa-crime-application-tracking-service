
plugins {
	id 'java'
	id "jacoco"
	id "org.sonarqube" version "6.2.0.5505"
	id "info.solidsoft.pitest" version "1.15.0"
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jsonschema2dataclass' version '6.0.0'
}

group = 'uk.gov.justice.laa.crime'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

def versions = [
		pitest                 			 : "1.16.1",
		okhttpVersion         			 : "4.9.3",
		mockwebserverVersion   			 : "4.12.0",
		sentryVersion					 : "8.20.0",
		validationapiVersion   			 : '2.0.1.Final',
		commonsioVersion     			 : '2.11.0',
		springdocVersion      			 : "2.8.8",
		commonsLang3Version   			 : "3.17.0",
		resilience4jVersion    			 : "2.2.0",
		postgresqlVersion     			 : "42.7.3",
		amazonSQSVersion                 : "2.1.1",
		springFrameworkCloudVersion      : "3.0.1",
		awsSdkStsVersion                 : "2.20.69",
		oauth2ResourceServer             : "3.4.1",
		securityCrypto                   : "6.4.4",
]
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation platform("io.sentry:sentry-bom:$versions.sentryVersion")

	implementation "io.sentry:sentry-spring-boot-starter-jakarta"
	implementation "io.sentry:sentry-logback"
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server:$versions.oauth2ResourceServer"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.security:spring-security-crypto:$versions.securityCrypto"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.springdocVersion"
	implementation "org.apache.commons:commons-lang3:$versions.commonsLang3Version"
	implementation "io.github.resilience4j:resilience4j-spring-boot3:$versions.resilience4jVersion"

	implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs:$versions.springFrameworkCloudVersion"
	implementation "org.springframework:spring-jms"
	implementation "com.amazonaws:amazon-sqs-java-messaging-lib:$versions.amazonSQSVersion"
	implementation "software.amazon.awssdk:sts:$versions.awsSdkStsVersion"

	implementation "org.postgresql:postgresql:$versions.postgresqlVersion"
	implementation "org.liquibase:liquibase-core"

	implementation "javax.validation:validation-api:$versions.validationapiVersion"

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation "com.h2database:h2"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.springframework.security:spring-security-test"
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "org.pitest:pitest:$versions.pitest"
	testImplementation "com.squareup.okhttp3:mockwebserver:$versions.mockwebserverVersion"
	testImplementation group: 'commons-io', name: 'commons-io', version: versions.commonsioVersion
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.11"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"**/model/**",
					"**/config/**",
					"**/exception/**"
			])
		}))
	}
}

pitest {
	targetClasses = [
			"uk.gov.justice.laa.crime.application.tracking.*",
			"uk.gov.justice.laa.crime.application.tracking.controller.*"
	]
	excludedClasses = [
			"uk.gov.justice.laa.crime.application.tracking.dto.*",
			"uk.gov.justice.laa.crime.application.tracking.mapper.*",
			"uk.gov.justice.laa.crime.application.tracking.CrimeApplicationTrackingApplication"
	]
	threads = 5
	outputFormats = ["XML", "HTML"]
	timestampedReports = false
	mutationThreshold = 60
}

tasks.named("sonar"){
	dependsOn test
}

sonar {
	properties {
		property "sonar.exclusions", "**/model/**.java , **/config/**.java, **/exception/**.java"
		property "sonar.coverage.exclusions", "**/CrimeApplicationTrackingApplication.java"
		property "sonar.coverage.jacoco.xmlReportPaths", "${project.layout.buildDirectory}/reports/jacoco/test/jacocoTestReport.xml"
		property "sonar.projectKey", "ministryofjustice_laa-crime-application-tracking-service"
		property "sonar.organization", "ministryofjustice"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.projectName", "Crime Apps :: crime-application-tracking"
		property "sonar.pitest.mode", "reuseReport"
		property "sonar.pitest.reportsDirectory", "build/reports/pitest"
	}
}

jsonSchema2Pojo {
	executions{
		main{
			io.source.setFrom files("${project.rootDir}/src/main/resources/schemas")
			klass.targetPackage = 'uk.gov.justice.laa.crime.application.tracking.model'
			klass.jackson2InclusionLevel = "NON_EMPTY"
			methods.builders = true
			methods.annotateJsr303 = true
			fields.floatUseBigDecimal = true
			dateTime.dateTimeType = "java.time.LocalDateTime"
		}
	}
}